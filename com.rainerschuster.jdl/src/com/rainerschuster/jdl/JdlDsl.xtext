grammar com.rainerschuster.jdl.JdlDsl with org.eclipse.xtext.common.Terminals

generate jdlDsl "http://www.rainerschuster.com/jdl/JdlDsl"

Model:
	elements+=Element*
;

Element:
	Entity
	| Enumeration
	| Relationship
	| Option
;

Entity:
	'entity' name=ID ('(' tableName=ID ')')? ('{' (fields+=Field (',' fields+=Field)*)? '}')?
;

Field:
	name=ID type=Type validation+=Validation*
;

Type:
	value='String'
	| value='Integer'
	| value='Long'
	| value='BigDecimal'
	| value='Float'
	| value='Double'
	| value='Enum'
	| value='Boolean'
	| value='LocalDate'
	| value='ZonedDateTime'
	| value='Blob'
	| value='AnyBlob'
	| value='ImageBlob'
	| value='Date' // Cassandra
	| value='UUID' // Cassandra
	| ref=[Element|ID]
;

Validation:
	name='required'
	| name='minlength' '(' minlength=INT ')'
	| name='maxlength' '(' maxlength=INT ')'
	| name='pattern' '(' pattern=STRING ')'
	| name='min' '(' min=INT ')'
	| name='max' '(' max=INT ')'
	| name='minbytes' '(' minbytes=INT ')'
	| name='maxbytes' '(' maxbytes=INT ')'
;

enum ReleationshipMultiplicity:
	OneToMany | ManyToOne | OneToOne | ManyToMany
;

Relationship:
	'relationship' multiplicity=ReleationshipMultiplicity '{' items+=RelationshipItem (',' items+=RelationshipItem)* '}'
;

RelationshipItem:
	from=[Entity|ID] ('{' fromFieldName=ID ('(' fromFieldJoin=[Field|ID] ')')? '}')? 'to' to=[Entity|ID] ('{' toFieldName=ID ('(' toFieldJoin=[Field|ID] ')')? '}')?
;

Enumeration:
	'enum' name=ID '{' items+=ID (',' items+=ID)* '}'
;

Option:
	DtoOption
	| PaginationOption
	| ServiceOption
	| SkipClientOption
	| SkipServerOption
	| AngularSuffixOption
	| MicroserviceOption
	| SearchOption
;

DtoOption:
	'dto' OptionEntitySelector 'with' value='mapstruct' OptionExcept?
;

ServiceOption:
	'service' OptionEntitySelector 'with' value=('serviceClass'|'serviceImpl') OptionExcept?
;

PaginationOption:
	'paginate' OptionEntitySelector 'with' value=('pager'|'pagination'|'infinite-scroll') OptionExcept?
;

// TODO search or searchEngine?
SearchOption:
	'search' OptionEntitySelector 'with' value='elasticsearch' OptionExcept?
;

SkipClientOption:
	'skipClient' 'for' OptionEntitySelector OptionExcept?
;

SkipServerOption:
	'skipServer' 'for' OptionEntitySelector OptionExcept?
;

AngularSuffixOption:
	'angularSuffix' OptionEntitySelector 'with' value=ID OptionExcept?
;

MicroserviceOption:
	'microservice' OptionEntitySelector 'with' value=ID OptionExcept?
;

fragment OptionEntitySelector*:
	all?=All
	| entities+=[Entity|ID] (',' entities+=[Entity|ID])*
;

fragment OptionExcept*:
	'except' except+=[Entity|ID] (',' except+=[Entity|ID])*
;

All:
	'*' | 'all'
;

terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_'|'-') ('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')*;
